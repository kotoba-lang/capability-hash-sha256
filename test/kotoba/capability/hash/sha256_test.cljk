(ns kotoba.capability.hash.sha256-test
  (:require [clojure.test :refer [deftest is]]
            [clojure.java.io :as io]
            [kotoba.capability.hash.sha256 :as capability]
            [kotoba.capability.hash.sha256.provider :as provider]
            [kotoba.core.capability-repository :as repository]
            [kotoba.core.contracts :as contracts])
  (:import [java.security MessageDigest]))

(defn- sha256-file [f]
  (let [md (MessageDigest/getInstance "SHA-256")
        bytes (.digest md (.readAllBytes (io/input-stream f)))]
    (apply str (map #(format "%02x" (bit-and % 0xff)) bytes))))

(deftest manifest-conforms-as-reference-implemented
  (is (= :reference-implemented (:capability/provider-status capability/manifest)))
  (is (= "hash/sha256" (:capability/id capability/manifest)))
  (is (= [] (repository/validate-manifest (contracts/capability-contract) capability/manifest))))

(deftest artifact-sha256-matches-bytes
  (let [path (io/file "artifacts/provider.core.wasm")
        declared (get-in capability/manifest [:capability/artifact :sha256])]
    (is (.isFile path))
    (is (= declared (sha256-file path)))))

(deftest artifact-exports-match-host-abi
  (let [exports (get-in capability/manifest [:capability/artifact :exports])
        abi (get-in capability/manifest [:capability/artifact :host-abi])]
    (is (= {"sha256_hex" {:params [:i32 :i32 :i32 :i32], :result :i32}} exports))
    (is (= {:module "kotoba", :field "sha256_hex"} abi))))

(deftest jvm-reference-provider-matches-nist-vectors
  (let [export (provider/host-export)
        f (:fn export)]
    (is (= "kotoba" (:module export)))
    (is (= "sha256_hex" (:field export)))
    (is (= [:i32 :i32 :i32 :i32] (:params export)))
    (is (= :i32 (:result export)))
    ;; empty string
    (is (= "e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855"
           (f "")))
    ;; "abc" (FIPS 180-2)
    (is (= "ba7816bf8f01cfea414140de5dae2223b00361a396177a9cb410ff61f20015ad"
           (f "abc")))
    ;; "hello" (matches kototama actor-host demo)
    (is (= "2cf24dba5fb0a30e26e83b2ac5b9e29e1b161e5c1fa7425e73043362938b9824"
           (f "hello")))
    ;; bytes path
    (is (= (f "hello") (f (.getBytes "hello" "UTF-8"))))))
