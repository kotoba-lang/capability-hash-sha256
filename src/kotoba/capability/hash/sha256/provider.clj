(ns kotoba.capability.hash.sha256.provider
  "JVM reference host provider for actor:host field \"sha256_hex\".

  Pure surface: `(digest-hex bytes-or-string) -> 64-char lowercase hex`.
  ABI metadata matches kototama.tender: (ptr len out-ptr out-cap) -> i32.
  Memory-backed host injection remains the embedder's job; this namespace
  proves the digest semantics with MessageDigest."
  (:import [java.security MessageDigest]
           [java.nio.charset StandardCharsets]))

(defn digest-hex
  "Lowercase hex SHA-256 of UTF-8 string or byte array."
  [input]
  (let [^bytes bs (cond
                    (string? input) (.getBytes ^String input StandardCharsets/UTF_8)
                    (bytes? input) input
                    :else (byte-array input))
        digest (.digest (MessageDigest/getInstance "SHA-256") bs)]
    (apply str (map #(format "%02x" (bit-and (int %) 0xff)) digest))))

(defn host-export
  "ABI shape for module \"kotoba\" field \"sha256_hex\".

  `:fn` is the pure digest helper (bytes/string → hex). Embedders that
  implement the memory ABI should write `(digest-hex bs)` into guest
  memory and return 64 (or -1 when out-cap < 64)."
  []
  {:module "kotoba"
   :field "sha256_hex"
   :params [:i32 :i32 :i32 :i32]
   :result :i32
   :fn digest-hex})
